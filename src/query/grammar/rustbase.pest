WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
object = {
    "{" ~ "}" |
    "{" ~ pair ~ ("," ~ pair)* ~ "}"
}
pair = { string ~ ":" ~ value }

array = {
    "[" ~ "]" |
    "[" ~ value ~ ("," ~ value)* ~ "]"
}
value = _{ object | array | string | number | boolean | null }

boolean = { "true" | "false" }

null = { "null" }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
json = _{ value }

d_get = _{ "get" }
d_set = _{ "set" }
d_update = _{ "update" | "upd" }
d_delete = _{ "delete" | "del" }

insert = { SOI ~ d_set ~ json ~ "in" ~ inner ~ EOI}
update = { SOI ~ d_update ~ json ~ "in" ~ inner ~ EOI}
delete = { SOI ~ d_delete ~ inner ~ EOI}
get = { SOI ~ d_get ~ inner ~ EOI}

crud = _{insert | update | delete | get}
