program = _{ SOI ~ "\n"* ~ (stmt ~ "&"+) * ~ stmt? ~ EOI }
stmt = _{ expr }

expr = {
      assgmtExpr
    | monadicExpr
    | intoExpr
    | sglExpr
    | terms
}

// exprs
assgmtExpr = { ident ~ "=" ~ expr }
monadicExpr = { keyword ~ verb ~ ((expr | ident)+)? }
intoExpr = { keyword ~ json ~ "into" ~ ident }
sglExpr = { keyword ~ ident? }

// terms
object = {
    "{" ~ "}" |
    "{" ~ pair ~ ("," ~ pair)* ~ "}"
}
pair = { string ~ ":" ~ value }

array = {
    "[" ~ "]" |
    "[" ~ value ~ ("," ~ value)* ~ "]"
}
value = _{ object | array | string | number | boolean | null }
boolean = { "true" | "false" }
null = { "null" }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
json = _{ value }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

terms = { term+ }
term = _{ json }

// verbs
verb = { "user" | "database" }

// keyword
keyword = { "insert" | "get" | "delete" | "update" | "list"}

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }